plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id "com.modrinth.minotaur" version "2.8.7"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

archivesBaseName = "${project_name}"

version = "${project_version}-1.20.1-Fabric"

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {}

dependencies {
    minecraft "com.mojang:minecraft:1.20.1"
    mappings loom.officialMojangMappings()

    modCompileOnly "net.fabricmc:fabric-loader:${fabric_loader_20}"
    modCompileOnly "net.fabricmc.fabric-api:fabric-api:${fabric_api_20}"

    modImplementation (include "me.lucko:fabric-permissions-api:${fabric_permissions_api_20}")
}

processResources {
    filesMatching('fabric.mod.json') {
        expand(
                'project_name': project_name,
                'project_version': project_version
        )
    }
}

def localProperties = new Properties()
def localPropertiesFile = new File(rootProject.file("/.gradle/gradle.properties").path)

if (localPropertiesFile.exists()) {
    localPropertiesFile.withInputStream { localProperties.load(it) }
}

modrinth {
    token = localProperties.getProperty("modrinth_api_key")
    projectId = "extralib"
    versionName = "${project_name}-${version}"
    versionNumber = project_version
    versionType = "release"
    changelog = rootProject.file("changelog.md").text
    uploadFile = remapJar
    gameVersions = ["1.20.1"]
    loaders = ["fabric"]
}

build.finalizedBy('versionedRelease')